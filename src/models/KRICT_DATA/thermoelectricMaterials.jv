// XSLXInterpeter does not work, see https://github.com/jvalue/jayvee/issues/603

// The paper mentions temperature to be between 10 & 1275 kelvin, see page 2
constraint TemperatureRange oftype RangeConstraint {
	lowerBound: 10.0;
	upperBound: 1275.0;
}
valuetype Temperature oftype decimal {
	constraints: [
		TemperatureRange
	];
}

// The paper mentions thermal conductivity to be between 0.07 & 77.16 W/mK , see page 2
constraint ThermalConductivityRange oftype RangeConstraint {
	lowerBound: 0.07;
	upperBound: 77.16;
}
valuetype ThermalConductivity oftype decimal {
	constraints: [
		ThermalConductivityRange
	];
}

pipeline ThermoElectricMaterialsPipeline {

	ThermoElectricMaterialsESTMExtractor
		-> ThermoElectricMaterialsESTMXLSXInterpreter
		-> ThermoElectricMaterialsESTMSheetPicker
		-> ThermoElectricMaterialsESTMTableInterpreter
		-> DOIStandardizerBlock
		-> ThermoElectricMaterialsESTMDatabaseLoader;

	block ThermoElectricMaterialsESTMExtractor oftype HttpExtractor {
		url: "https://github.com/KRICT-DATA/SIMD/raw/main/dataset/estm.xlsx";
	}

	block ThermoElectricMaterialsESTMXLSXInterpreter oftype XLSXInterpreter { }


	block ThermoElectricMaterialsESTMSheetPicker oftype SheetPicker {
		sheetName: "Sheet1";
	}

	block DOIStandardizerBlock oftype DOIStandardizer {
		doiColumn: 'reference';
	}

	block ThermoElectricMaterialsESTMTableInterpreter oftype TableInterpreter {
		header: false;
		columns: [
			'Formula' oftype text,
			'temperature_k' oftype Temperature,
			'seebeck_coefficient(Î¼V/K)' oftype decimal,
			'electrical_conductivity(S/m)' oftype decimal,
			'thermal_conductivity(W/mK)' oftype ThermalConductivity,
			'power_factor(W/mK^2)' oftype text,
			'ZT' oftype text,
			'reference' oftype text,
		];
	}

	block ThermoElectricMaterialsESTMDatabaseLoader oftype SQLiteLoader {
		table: "ESTM";
		file: "./ThermoElectricMaterialsDatabase.sqlite";
	}
}

use {
	BracesRemover
} from "./../../shared/composite-blocktypes.jv";
use {
	RemoveOpeningParenthesis
} from "./../../shared/transforms.jv";

pipeline SemiconductorBandGapsPipeline {

	SemiconductorBandGapsExtractor
		-> ZipArchiveInterpreter
		-> SemiconductorBandGapsCSVPicker
		-> SemiconductorBandGapsTextFileInterpreter
		-> SemiconductorBandGapsCSVInterpreter
		-> SemiconductorBandGapsTableInterpreter
		-> ValueBracesRemover
		-> TemperatureBracesRemover
		-> BracesRemoverRawUnit
		-> BracesRemoverTemperatureRawUnit
		-> SemiconductorBandGapsDatabaseLoader;

	block SemiconductorBandGapsExtractor oftype HttpExtractor {
		url: "https://figshare.com/ndownloader/files/31781996";
	}

	block ZipArchiveInterpreter oftype ArchiveInterpreter {
		archiveType: "zip";
	}

	block SemiconductorBandGapsCSVPicker oftype FilePicker {
		path: "/Bandpgap_database_v2/Bandgap.csv";
	}

	block SemiconductorBandGapsTextFileInterpreter oftype TextFileInterpreter { }

	block SemiconductorBandGapsCSVInterpreter oftype CSVInterpreter {
		delimiter: ",";
	}


	block ValueBracesRemover oftype BracesRemover {
		columnWithBraces: 'Value';
	}

	block TemperatureBracesRemover oftype BracesRemover {
		columnWithBraces: 'Temperature_value';
	}

	block BracesRemoverRawUnit oftype TableTransformer {
		inputColumns: [
			'Raw_unit'
		];
		outputColumn: 'Raw_unit';
		uses: RemoveOpeningParenthesis;
	}

	block BracesRemoverTemperatureRawUnit oftype TableTransformer {
		inputColumns: [
			'Temperature_raw_unit'
		];
		outputColumn: 'Temperature_raw_unit';
		uses: RemoveOpeningParenthesis;
	}

	block SemiconductorBandGapsTableInterpreter oftype TableInterpreter {
		header: true;
		columns: [
			"Name" oftype text,
			"Composition" oftype text,
			"Value" oftype text,
			"Unit" oftype text,
			"Raw_value" oftype text,
			"Raw_unit" oftype text,
			"Snowball" oftype boolean,
			"Confidence" oftype decimal,
			"AutoSentenceParser" oftype boolean,
			"BandgapDB" oftype boolean,
			"Text" oftype text,
			"Publisher" oftype text,
			"DOI" oftype DOI,
			"Temperature_value" oftype text,
			"Temperature_unit" oftype TemperatureUnitKelvin,
			"Temperature_raw_value" oftype decimal,
			"Temperature_raw_unit" oftype text,
			"Notes" oftype text,
		];
	}

	block SemiconductorBandGapsDatabaseLoader oftype SQLiteLoader {
		table: "SemiConductorBandGaps";
		file: "./SemiConductorBandGaps.sqlite";
	}
}
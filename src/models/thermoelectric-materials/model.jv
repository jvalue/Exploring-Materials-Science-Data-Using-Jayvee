use {
	RemoveOpeningBrace,
	RemoveClosingBrace
} from "./../../shared/transforms.jv";


/*
	Each record contains a chemical entity and one of the seminal thermoelectric properties:
	- Thermoelectric figure of merit
	- ZT
	- thermal conductivity, κ
	- Seebeck coefcient, S
	- electrical conductivity, σ
	- power factor, PF
*/
constraint AllowedModels oftype AllowlistConstraint {
	allowlist: [
		"Conductivity",
		"ThermCond",
		"Seebeck",
		"PF",
		"ZT"
	];
}
valuetype Model oftype text {
	constraints: [
		AllowedModels
	];
}

// The thermal conductivity has been mentioned to be only in (total, electronic, and lattice contributions), see page 3.
constraint AllowedModelTypes oftype AllowlistConstraint {
	allowlist: [
		"electronic",
		"lattice",
		"total"
	];
}
valuetype ModelType oftype text {
	constraints: [
		AllowedModelTypes
	];
}


// Access type of the source paper, has to be open or payment according to the paper.
constraint AllowedAccessTypes oftype AllowlistConstraint {
	allowlist: [
		"open",
		"payment"
	];
}
valuetype AccessType oftype text {
	constraints: [
		AllowedAccessTypes
	];
}


pipeline ThermoelectricMaterialsPipeline {

	ThermoelectricMaterialsExtractor
		-> ZipArchiveInterpreter
		-> InfThermoelectricMaterialsCSVPicker
		-> InfThermoelectricMaterialsTextFileInterpreter
		-> InfThermoelectricMaterialsCSVInterpreter
		-> InfThermoelectricMaterialsTableInterpreter
		-> InfThermoelectricMaterialsDatabaseLoader;
	ZipArchiveInterpreter
		-> MainThermoelectricMaterialsCSVPicker
		-> MainThermoelectricMaterialsTextFileInterpreter
		-> MainThermoelectricMaterialsCSVInterpreter
		-> MainThermoelectricMaterialsTableInterpreter
		-> OpeningBracesRemover
		-> ClosingBracesRemover
		-> MainThermoelectricMaterialsDatabaseLoader;

	block ThermoelectricMaterialsExtractor oftype HttpExtractor {
		url: "https://figshare.com/ndownloader/files/36554916";
	}

	block ZipArchiveInterpreter oftype ArchiveInterpreter {
		archiveType: "zip";
	}

	block InfThermoelectricMaterialsCSVPicker oftype FilePicker {
		path: "/TE_databases/CSV/inf_tedb.csv";
	}

	block MainThermoelectricMaterialsCSVPicker oftype FilePicker {
		path: "/TE_databases/CSV/main_tedb.csv";
	}

	block InfThermoelectricMaterialsTextFileInterpreter oftype TextFileInterpreter { }

	block MainThermoelectricMaterialsTextFileInterpreter oftype TextFileInterpreter { }

	block InfThermoelectricMaterialsCSVInterpreter oftype CSVInterpreter {
		delimiter: ",";
	}

	block MainThermoelectricMaterialsCSVInterpreter oftype CSVInterpreter {
		delimiter: ",";
	}

	block OpeningBracesRemover oftype TableTransformer {
		inputColumns: [
			'Value'
		];
		outputColumn: 'Value';
		uses: RemoveOpeningBrace;
	}

	block ClosingBracesRemover oftype TableTransformer {
		inputColumns: [
			'Value'
		];
		outputColumn: 'Value';
		uses: RemoveClosingBrace;
	}

	block InfThermoelectricMaterialsTableInterpreter oftype TableInterpreter {
		header: true;
		columns: [
			"Name" oftype text,
			"Temperature_Value" oftype decimal,
			"ZT" oftype text,
			"PF" oftype text,
			"S^2" oftype text,
			"s" oftype text,
			"k" oftype text,
			"Original_Counts" oftype integer,
			"New_Counts" oftype integer,
			"Inference" oftype text,
			"Editing" oftype text,
			"Pressure" oftype text,
			"Process" oftype text,
			"Label" oftype text,
			"Direction_of_Measurement" oftype text,
			"DOI" oftype DOI,
			"Publisher" oftype text,
			"Access_Type" oftype text,
			"Publication_Year" oftype integer,
			"Authors" oftype text,
			"Journal" oftype text,
			"Comparison" oftype text,
		];
	}

	block MainThermoelectricMaterialsTableInterpreter oftype TableInterpreter {
		header: true;
		columns: [
			"Name" oftype text,
			"Label" oftype text,
			"Editing" oftype text,
			"Model" oftype Model,
			"Model_Type" oftype text,
			"Specifier" oftype text,
			"Value" oftype text,
			"Units" oftype text,
			"Temperature_Value" oftype text,
			"Temperature_Units" oftype TemperatureUnitKelvin,
			"Value_Average" oftype text,
			"Temperature_Average" oftype decimal,
			"Pressure" oftype text,
			"Process" oftype text,
			"Direction_of_Measurement" oftype text,
			"Resolution" oftype text,
			"DOI" oftype DOI,
			"Title" oftype text,
			"Access_Type" oftype text,
			"Publisher" oftype text,
			"Publication_Year" oftype integer,
			"Authors" oftype text,
			"Journal" oftype text,
		];
	}

	block InfThermoelectricMaterialsDatabaseLoader oftype SQLiteLoader {
		table: "InfThermoelectricMaterials";
		file: "./ThermoelectricMaterials.sqlite";
	}

	block MainThermoelectricMaterialsDatabaseLoader oftype SQLiteLoader {
		table: "MainThermoelectricMaterials";
		file: "./ThermoelectricMaterials.sqlite";
	}
}
